/*
 * The sequence of triangle numbers is generated by adding the natural numbers. So
 * the 7^th triangle number would be 1 + 2 + 3 + 4 + 5 + 6 + 7 = 28. The first ten
 * terms would be:
 *
 * 1, 3, 6, 10, 15, 21, 28, 36, 45, 55, ...
 *
 * Let us list the factors of the first seven triangle numbers:
 *
 *      1: 1
 *      3: 1,3
 *      6: 1,2,3,6
 *     10: 1,2,5,10
 *     15: 1,3,5,15
 *     21: 1,3,7,21
 *     28: 1,2,4,7,14,28
 *
 * We can see that 28 is the first triangle number to have over five divisors.
 *
 * What is the value of the first triangle number to have over five hundred
 * divisors?
 */

.cstring
format: .asciz "%ld\n"

.text
.globl _main

_count_divisors:
    movq $1, %rsi
    movq $1, %rcx
    movq $2, %r8
0:
    cmpq %rdi, %r8
    ja 2f
    movq %rdi, %rax
    xorq %rdx, %rdx
    divq %r8
    testq %rdx, %rdx
    jz 1f
    movq %rsi, %rax
    mulq %rcx
    movq %rax, %rsi
    movq $1, %rcx
    incq %r8
    jmp 0b
1:
    movq %rax, %rdi
    incq %rcx
    jmp 0b
2:
    movq %rsi, %rax
    mulq %rcx
    retq

_main:
    pushq %rbp
    movq %rsp, %rbp
    movq $1, %r12
    movq $2, %r13
0:
    addq %r13, %r12
    incq %r13
    movq %r12, %rdi
    callq _count_divisors
    cmpq $500, %rax
    jb 0b
    leaq format(%rip), %rdi
    movq %r12, %rsi
    callq _printf
    popq %rbp
    xorq %rax, %rax
    retq
