;;; Each new term in the Fibonacci sequence is generated by adding the previous two
;;; terms. By starting with 1 and 2, the first 10 terms will be:
;;;
;;; 1, 2, 3, 5, 8, 13, 21, 34, 55, 89, ...
;;;
;;; By considering the terms in the Fibonacci sequence whose values do not exceed
;;; four million, find the sum of the even-valued terms.

(use-modules (srfi srfi-26)
             (srfi srfi-41))

(define fibonacci
  (stream-cons 1
               (stream-cons 1
                            (stream-map + (stream-cdr fibonacci) fibonacci))))

(display
  ((compose (cut stream-fold + 0 <>)
            (cut stream-filter even? <>)
            (cut stream-take-while (cut < <> 4e6) <>))
   fibonacci))
(newline)
